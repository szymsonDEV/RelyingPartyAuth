buildscript {
    ext {
        springBootVersion = '2.7.3'
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

Properties versions = new Properties()
versions.load(new FileInputStream(rootProject.projectDir.path + "/version.properties"))

rootProject.version = versions.version
rootProject.group = 'dev.szymson.example'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'
apply plugin: 'groovy'

springBoot {
    buildInfo {
        properties {
            additional = [
                    'by'                   : System.properties['user.name'],
                    'operatingSystem'      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    'continuousIntegration': System.getenv('CI') ? true : false,
                    'machine'              : InetAddress.localHost.hostName,
                    'time'                 : new Date(),
                    'buildTag'             : System.getenv('BUILD_TAG') ? System.getenv('BUILD_TAG') : 'Undefined',
                    'gitCommit'            : System.getenv('GIT_COMMIT') ? System.getenv('GIT_COMMIT') : 'Undefined'
            ]
        }
    }
}

war {
    enabled = true
    archiveBaseName.set(rootProject.name);
    archiveClassifier = ''
}

task buildApplication {
    group = "Build"
    dependsOn war
}

sourceCompatibility = 18
targetCompatibility = 18
compileJava {
    options.encoding = "UTF-8"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.yubico:webauthn-server-core:2.0.0'
    implementation 'com.yubico:webauthn-server-attestation:2.0.0'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.projectlombok:lombok:1.18.24'
    annotationProcessor "org.projectlombok:lombok",
            "org.springframework.boot:spring-boot-configuration-processor"
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    providedRuntime("org.apache.tomcat.embed:tomcat-embed-core:9.0.63")
    runtimeOnly 'com.h2database:h2:2.1.214'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.2'
    testImplementation 'org.springframework.security:spring-security-test:5.7.2'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

bootRun {
    mainClass = 'dev.szymson.relyingpartyauth.RelyingPartyAuthApplication'
    jvmArgs = [
            "-Dspring.profiles.active=dev",
    ]
}